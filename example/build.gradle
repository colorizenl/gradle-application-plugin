buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath files("../build/libs/application-plugin.jar")
        classpath "org.jsoup:jsoup:1.22.1"
        classpath "org.commonmark:commonmark:0.27.0"
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.53.0"
    id "com.gradleup.shadow" version "9.3.1"
}

apply plugin: "java"
apply plugin: "nl.colorize.gradle.application"

group = "nl.colorize"
version = "0.1"
compileJava.options.encoding = "UTF-8"

java {
    sourceCompatibility = "25"
    targetCompatibility = "25"
    sourceSets.main.java.srcDirs = ["source"]
    sourceSets.main.resources.srcDirs = ["../resources"]
    sourceSets.test.java.srcDirs = ["source"]
}

macApplicationBundle {
    name = "Example"
    identifier = "com.example"
    bundleVersion = "0.1"
    description = "A simple example application"
    copyright = "Copyright 2010-2024 Colorize"
    icon = "../resources/icon.icns"
    applicationCategory = "public.app-category.developer-tools"
    mainClassName = "com.example.ExampleApp"
    mainJarName = "example-shadow.jar"
}

msi {
    inherit = true
    mainJarName = "example.jar"
    vendor = "Colorize"
    icon = "../resources/icon.ico"
    uuid = "b9112b5f-2340-4541-8883-6abd3c9c8780"
}

exe {
    inherit = true
    mainJarName = "example.jar"
    icon = "${projectDir}/../resources/icon.ico"
    supportURL = "https://www.colorize.nl"
}

xcode {
    appId = "Example"
    bundleId = "com.example"
    appName = "Example App"
    bundleVersion = "0.1"
    icon = "../resources/icon.png"
    resourcesDir = "${buildDir}/staticsite"
}

pwa {
    webAppDir = "${buildDir}/staticsite"
    manifest = "web/manifest.json"
    cacheName = "example"
}

staticSite {
    contentDir = "web"
}

appIcon {
    original = "../resources/icon.png"
}

generateStaticSite.doLast {
    copy {
        from "../resources/icon.png"
        into "${buildDir}/staticsite/images"
    }
}

shadowJar {
    archiveFileName = "example-shadow.jar"
    manifest {
        attributes "Application-Name": "Example"
        attributes "Main-Class": "com.example.ExampleApp"
    }
}

generatePWA.dependsOn generateStaticSite
xcodeGen.dependsOn generateStaticSite
