buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.12.0"
    }
}

apply plugin: "java-gradle-plugin"
apply plugin: "com.gradle.plugin-publish"
apply plugin: "jacoco"

group = "nl.colorize"
version = "2022.1"
sourceCompatibility = "11"
targetCompatibility = "11"
compileJava.options.encoding = "UTF-8"
sourceSets.main.java.srcDirs = ["source"]
sourceSets.main.resources.srcDirs = ["resources"]
sourceSets.test.java.srcDirs = ["test"]
jar.archiveFileName = "application-plugin-${version}.jar"
jar.duplicatesStrategy = DuplicatesStrategy.INCLUDE

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation files("lib/appbundler.jar")
    testImplementation "org.junit.jupiter:junit-jupiter:5.8.2"
}

jar {
    from {
        configurations.runtimeClasspath
            .filter { it.toString().endsWith("appbundler.jar") }
            .collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    plugins {
        applicationPlugin {
            id = "nl.colorize.gradle.application"
            implementationClass = "nl.colorize.gradle.application.ApplicationPlugin"
        }
    }
}

pluginBundle {
    website = "http://www.colorize.nl/en/"
    vcsUrl = "https://github.com/colorizenl/gradle-application-plugin"
    description = "Gradle plugin to build native applications for Mac, iOS, Android, and Windows."
    tags = ["application", "mac", "macos", "android", "windows", "application bundle"]
    
    plugins {
        applicationPlugin {
            id = "nl.colorize.gradle.application"
            displayName = "Gradle application plugin"
        }
    }
    
    mavenCoordinates {
        groupId = "nl.colorize"
        artifactId = "gradle-application-plugin"
    }
}

task coverage(dependsOn: ["test", "jacocoTestReport"]) {
    doLast {
        def report = file("${buildDir}/reports/jacoco/test/html/index.html").text
        def coverage = report =~ "tfoot.*?ctr2.*?>([\\d.]+%).*?tfoot"
        println "-" * 40
        println "Test coverage: " + coverage[0][1]
    }
}

task checkSigningIdentity() {
    doLast {
        println project.property("macApplicationBundle.identity.app")
        println project.property("macApplicationBundle.identity.installer")
    }
}
